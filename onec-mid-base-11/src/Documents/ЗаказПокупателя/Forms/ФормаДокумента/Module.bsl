
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//++ Начало доработки
	//Вывод реквизита "КонтактноеЛицо" на форму документа
	
	СледующийЭлемент = Элементы.Найти("ГруппаШапкаПраво");	
	НовыйЭлемент = Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"),СледующийЭлемент);		
	//Затем донастраиваем его и указываем нужные свойства
	НовыйЭлемент.ПутьКДанным = "Объект.КонтактноеЛицо";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = "Контактное лицо";
	
	//--КонецДоработки
	
	//++ Начало доработки
	//Создание команды "ПрименитьСкидку", создание Группы на форме документа
	СледующийЭлемент = Элементы.Найти("ГруппаШапкаЛево");
	Группа = Элементы.Добавить("ГруппаРасчетСкидки",Тип("ГруппаФормы"),СледующийЭлемент);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Заголовок = "";
	
	Команда = Команды.Добавить("ПрименитьСкидку"); //Имя команды
	Команда.Заголовок = "Применить скидку";
	Команда.Действие  = "КомандаПрименитьСкидку"; //Имя связанной процедуры
		
	//Добавление кнопки формы
	СледующийЭлемент = Элементы.Найти("ГруппаРасчетСкидки");
	КнопкаФормы = Элементы.Добавить("КомандаПрименитьСкидку", Тип("КнопкаФормы"), СледующийЭлемент);		
	КнопкаФормы.ИмяКоманды = "ПрименитьСкидку"; //Связь с командой по имени
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
	
    //--КонецДоработки
	//++ Начало доработки
	//Вывод реквизита "РазмерСкидки"
	СледующийЭлемент = Элементы.Найти("ГруппаРасчетСкидки");	
	
	НовыйЭлемент = Элементы.Добавить("РазмерСкидки", Тип("ПолеФормы"),СледующийЭлемент);		
	НовыйЭлемент.ПутьКДанным = "Объект.РазмерСкидки";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = "Размер скидки";
	
	Элементы.РазмерСкидки.УстановитьДействие("ПриИзменении", "РазмерСкидкиПриИзменении")
	//--КонецДоработки
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПрименитьСкидку(Команда)
	//++ Начало доработки
	Для Каждого СтрокаТовара Из Объект.Товары Цикл 
		РассчитатьСкидку(СтрокаТовара);
	КонецЦикла;
	Для Каждого СтрокаТовара Из Объект.Услуги Цикл 
		РассчитатьСкидку(СтрокаТовара);
	КонецЦикла;	
	//--КонецДоработки
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСкидку(СтрокаТовары)
	//++ Начало доработки
	Сумма = СтрокаТовары.Цена * СтрокаТовары.Количество;
	РазмерСкидки = (Сумма * Объект.РазмерСкидки) / 100;
	СтрокаТовары.Сумма = Сумма - РазмерСкидки;
	//--КонецДоработки
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//++ Начало доработки
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	РассчитатьСкидку(ТекущиеДанные);
	//--Конец доработки
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Асинх Процедура РазмерСкидкиПриИзменении(Элемент)
	//++ Начало доработки
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Значение в табличной части изменилось. Необходимо сделать пересчет скидок?';"), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	Иначе
		Для Каждого СтрокаТовара Из Объект.Товары Цикл 
			РассчитатьСкидку(СтрокаТовара);
		КонецЦикла;
		Для Каждого СтрокаТовара Из Объект.Услуги Цикл 
			РассчитатьСкидку(СтрокаТовара);
		КонецЦикла;	
	КонецЕсли;
	РассчитатьСуммуДокумента();
	//++ Конец доработки
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
